plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
apply from: '../secret.gradle'


android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.image_calculator"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        camera {
            storeFile file(secretCamera.fileName)
            storePassword secretCamera.storePassword
            keyAlias secretCamera.keyAlias
            keyPassword secretCamera.keyPassword
        }

        fileSystem {
            storeFile file(secretFileSystem.fileName)
            storePassword secretFileSystem.storePassword
            keyAlias secretFileSystem.keyAlias
            keyPassword secretFileSystem.keyPassword
        }
    }

    buildTypes {
        camera {
            signingConfig signingConfigs.camera
            debuggable true
            applicationIdSuffix ".camera"
            versionNameSuffix "-camera"
        }

        fileSystem {
            signingConfig signingConfigs.fileSystem
            debuggable true
            applicationIdSuffix ".filesystem"
            versionNameSuffix "-filesystem"
        }
    }

    flavorDimensions 'default'

    productFlavors {
        red {
        }
        green {
        }
    }

    variantFilter { variant ->

        if(variant.buildType.name == 'release') {
            variant.setIgnore(true)
        }

        if(variant.buildType.name == 'debug') {
            variant.setIgnore(true)
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.3"


    // For image recognition
    implementation "com.google.android.gms:play-services-mlkit-text-recognition:18.0.2"

    // For permission dispatcher
    implementation "com.github.permissions-dispatcher:ktx:1.1.4"
}